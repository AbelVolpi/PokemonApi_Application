plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.hilt.android
    alias libs.plugins.kotlin.kapt
}

apply from: '../tasks/jacoco-modules.gradle'

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.abelvolpi.pokemonapi"
        namespace 'com.abelvolpi.pokemonapi'
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.abelvolpi.pokemonapi.CustomTestRunner"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
            merges += "META-INF/LICENSE.md"
            merges += "META-INF/LICENSE-notice.md"
        }
    }
}

dependencies {
    // Android Core
    implementation libs.androidx.core
    implementation libs.appcompat
    implementation libs.material
    implementation libs.constraintlayout
    implementation libs.legacy.support

    // Project Modules
    implementation project(":presentation")
    implementation project(":domain")
    implementation project(":data")

    //Hilt - Dependency Injection
    implementation libs.hilt
    kapt libs.hilt.compiler

    // UI Tests
    androidTestImplementation project(":common-test")

    //Hilt - UI Tests
    androidTestImplementation libs.hilt.android.testing
    kaptAndroidTest libs.hilt.compiler

    // Fragment Testing - UI tests
    debugImplementation libs.androidx.fragment.testing.manifest
    androidTestImplementation libs.androidx.fragment.testing

    // Espresso - UI tests
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation libs.androidx.espresso.contrib
    androidTestImplementation libs.androidx.espresso.idling.resource

    // Mock Calls - UI tests
    androidTestImplementation libs.mockwebserver
    androidTestImplementation libs.androidx.junit
}

kapt {
    correctErrorTypes = true
}